;[inverted] = _TRUE_
;[export] = _TRUE_
;[exportTTS] = _TRUE_
;[exportfolder] = .\output\tts\cards
[card_width] = 9.2
[card_height] = 5.9
;[custom_border_width_round]% = 9
;[custom_border_height_round]% = 7

[export]% = _FALSE_
[exportTTS]% = _FALSE_
[exportfolder]% = .\output\tts
[backsonly]% = _FALSE_
[backs]% = _FALSE_
[duplexbacks]% = _FALSE_
[border_guidelines]% = MARKDOT
[border_type] = RECTANGLE
[border_type]%[exportTTS] = ROUNDED
PAGE = [A4PORTRAIT]
INCLUDE = init.nde

LINK = "projects.csv", firstCost, firstType, secondCost, secondType, thirdCost, thirdType, fourthCost, fourthType, gain, gainType, requirespassedhalf, requirements, points, variablePoints, projectType, name, gainoffsety, artpath, artx, arty, artw, arth, artontop,starcoverfrom,uid,nofplayers
[nofCards] = {(points)}
[PROJECTSRANGE] = "1-{[nofCards]}"
[PROJECTSBACKSRANGE] = "{[nofCards]+1}-{[nofCards]*2}"
[ART] = {artpath?§}{name?§}[IMAGE_FILE_TYPE]
[BORDERTOP] = [PROJECTS_CARD_ART]{projectType?§}background[IMAGE_FILE_TYPE]
[BORDERBOTTOM] = [PROJECTS_CARD_ART]{projectType?§}costs[IMAGE_FILE_TYPE]
[GAINBKG] = [PROJECTS_CARD_ART]immediategainbackground[IMAGE_FILE_TYPE]

;;;;;;;;;;;;;;;;;;;;;
;;; CARD BACK FOR TTS
;;;;;;;;;;;;;;;;;;;;;
MACRO = PROJECTBACK, (r)
   FONT = [text_font], 16, "TB", [black]
   RECTANGLE = (r), 0, 0, 100%, 100%, [grayB]
   TEXT = (r), "PROJECT", 0, 2, 100%, 2, CENTER, CENTER
END

IF = [exportTTS]
   PROJECTBACK = 1
   EXPORTFULL = 1, "projectsback"
ENDIF

RECTANGLE = [PROJECTSRANGE], 0, 0, 100%, 100%, [white]

;;;;;;;;;;;;;;;;;;;;;
;;; CARD BACKGROUND
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IMAGE = [PROJECTSRANGE], [PROJECTS_CARD_ART]stars[IMAGE_FILE_TYPE], 0, 0, 9.2, 5.9, 0, N

   IF=[starcoverfrom]<6
      RECTANGLE = [PROJECTSRANGE], 0, [starcoverfrom], 9.2, {5-[starcoverfrom]}, [black]
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; CARD ART
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IF=[artontop]=N
      IMAGE = [PROJECTSRANGE], [ART], [artx], [arty], [artw], [arth], 0, N
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; TOP BORDER
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IMAGE = [PROJECTSRANGE], [BORDERTOP], 0, 0, 9.2, 2, 0, N
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; PROJECT NAME
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   FONT = [FONT_project_name]
   TEXT = [PROJECTSRANGE], [name], 1.4, 0.5, 6.4, 0.8, CENTER, CENTER
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; PROJECT TYPE
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IF=[projectType]=infrastructure
      ELLIPSE = [PROJECTSRANGE], 0.5, 0.5, 0.8, 0.8, [black],[infrastructure], 0.02
      ICONS = [PROJECTSRANGE], I, 0.65, 0.65, 1, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
   ELSEIF=[projectType]=security
      ELLIPSE = [PROJECTSRANGE], 0.5, 0.5, 0.8, 0.8, [black],[security], 0.02
      ICONS = [PROJECTSRANGE], S, 0.65, 0.65, 1, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
   ELSEIF=[projectType]=science
      ELLIPSE = [PROJECTSRANGE], 0.5, 0.5, 0.8, 0.8, [black],[science], 0.02
      ICONS = [PROJECTSRANGE], E, 0.65, 0.65, 1, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
   ELSE
      ELLIPSE = [PROJECTSRANGE], 0.5, 0.5, 0.8, 0.8, [black],[commerce], 0.02
      ICONS = [PROJECTSRANGE], C, 0.65, 0.65, 1, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; POINTS
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   ;ELLIPSE = [PROJECTSRANGE], 7.9, 0.5, 0.8, 0.8, [black], [pointsbackground], 0.02
   IF=([points]=J) _OR_ ([points]=T) _OR_ ([points]=F) _OR_ ([points]=D)
      IMAGE = [PROJECTSRANGE], "[PATH_TO_IMAGES]vpX[IMAGE_FILE_TYPE]", 7.8, 0.4, 1, 1, 0, NP
      ;FONT = [FONT_project_dynamicpoints]
      ;TEXT = [PROJECTSRANGE], *, 7.91, 0.5, 0.8, 0.8, CENTER, TOP
   ELSE
      ;FONT = [FONT_project_points]
      ;TEXT = [PROJECTSRANGE], [points], 7.91, 0.57, 0.8, 0.8, CENTER, TOP
      IMAGE = [PROJECTSRANGE], "[PATH_TO_IMAGES]vp[points][IMAGE_FILE_TYPE]", 7.8, 0.4, 1, 1, 0, NP
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; IMMEDIATE GAIN
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IF=[gain]>0
      ROUNDRECT = [PROJECTSRANGE], 7.9, {1.74+[gainoffsety]}, 1, 1, [[projectType]]
      ;IMAGE = [PROJECTSRANGE], [GAINBKG], 7.7, {1.74+[gainoffsety]}, 1.2, 1.0 , 0, N
      ;FONT = [FONT_project_return]
      PRINTRESOURCE = [PROJECTSRANGE], 8.1, {1.89+{gainoffsety?§}}, [gain], [gainType]
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; VARIABLE POINTS EXPLAINED
;;;;;;;;;;;;;;;;;;;;;
[variable_points_explained_x1] = 5.48
[variable_points_explained_x2] = 5.92
[variable_points_explained_x3] = 6.04

IF = _NOT_ [backsonly]
   IF=([points]=J) _OR_ ([points]=T) _OR_ ([points]=F) _OR_ ([points]=D)
      FONT = [FONT_project_dynamicpoints_inv]
      TEXT = [PROJECTSRANGE], *, [variable_points_explained_x1], {2.74 + [gainoffsety]}, 0.8, 0.8, LEFT, TOP
      FONT = [FONT_project_points_explained]
      TEXT = [PROJECTSRANGE], "= [variablePoints] points per", [variable_points_explained_x2], {2.92+[gainoffsety]}, 3.16, 0.4, LEFT, TOP
      ICONS = [PROJECTSRANGE], [points], [variable_points_explained_x1], {3.34+[gainoffsety]}, 0.5, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
      TEXT = [PROJECTSRANGE], construction, [variable_points_explained_x3], {3.36+[gainoffsety]}, 3.04, 0.4, LEFT, TOP
      IF=[points]=F
         TEXT = [PROJECTSRANGE], "(including this)", [variable_points_explained_x1], {3.86+[gainoffsety]}, 2.6, 0.4, LEFT, TOP
      ENDIF
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; COSTS
;;;;;;;;;;;;;;;;;;;;;
[costspace] = 0.8
[cost1x] = 1.25
[cost2x] = [cost1x] + [costspace]
[cost3x] = [cost2x] + [costspace]
[cost4x] = [cost3x] + [costspace]

IF = _NOT_ [backsonly]
   FONT = [FONT_project_cost]
   ;IMAGE = [PROJECTSRANGE], [BORDERBOTTOM], 0, 4.6, 9.2, 1.3, 0, N
   RECTANGLE = [PROJECTSRANGE], 0, 4.6, 9.2, 1.3, [[projectType]]

   IF=[firstCost]>0
      PRINTRESOURCE = [PROJECTSRANGE], [cost1x], 4.8, [firstCost], [firstType]
   ENDIF
   IF=[secondCost]>0
      PRINTRESOURCE = [PROJECTSRANGE], [cost2x], 4.8, [secondCost], [secondType]
   ENDIF
   IF=[thirdCost]>0
      PRINTRESOURCE = [PROJECTSRANGE], [cost3x], 4.8, [thirdCost], [thirdType]
   ENDIF
   IF=[fourthCost]>0
      PRINTRESOURCE = [PROJECTSRANGE], [cost4x], 4.8, [fourthCost], [fourthType]
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; REQUIREMENTS
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IF=[requirespassedhalf]=Y
      ICONS = [PROJECTSRANGE], H, 5.6, 5, 0.5, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
      ICONS = [PROJECTSRANGE], [requirements], 6.15, 5, 2.55, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
   ELSE
      ICONS = [PROJECTSRANGE], [requirements], 5.6, 5, 9, 0.5, 0.5, 0.5, 0, N, LEFT, TOP
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; RESERVATION
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   ;POLYGON = [PROJECTSRANGE], 4.48, 4.68, 1, 1, 8, 22.5, [black], EMPTY, 0.04
   IMAGE = [PROJECTSRANGE], "[PATH_TO_IMAGES]octagon[IMAGE_FILE_TYPE]", 4.28, 4.48, 1.4, 1.4, 0, N
   IMAGE = [PROJECTSRANGE], "[PATH_TO_IMAGES]padlock_with_key[IMAGE_FILE_TYPE]", 4.68, 4.78, 0.6, 0.8, 0, N
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; CARD ART
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   IF=[artontop]=Y
      IMAGE = [PROJECTSRANGE], [ART], [artx], [arty], [artw], [arth], 0, N
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; PLAYERS
;;;;;;;;;;;;;;;;;;;;;
FONT = [FONT_nof_players]
IF=_NOT_ ({nofplayers?§}=0)
   TEXT = [PROJECTSRANGE], [nofplayers], 0.6, 4.9, 2, 0.8, LEFT, BOTTOM
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; UID
;;;;;;;;;;;;;;;;;;;;;
IF = _NOT_ [backsonly]
   FONT = [FONT_uid_white]
   TEXT = [PROJECTSRANGE], [uid], 8.1, 0.1, 0.6, 0.8, LEFT, TOP

   IF = [exportTTS]
      DISPLAY = "[exportfolder]\projects.png", 1, 16, 4
   ENDIF
ENDIF

;;;;;;;;;;;;;;;;;;;;;
;;; BACKS
;;;;;;;;;;;;;;;;;;;;;
IF = [backsonly]
   PROJECTBACK = [PROJECTSRANGE]
ELSE
   IF = _NOT_ [exportTTS]
      IF = [backs] _OR_ [duplexbacks]
         PROJECTBACK = [PROJECTSBACKSRANGE]
      ENDIF

      IF = [duplexbacks]
         DUPLEX = [PROJECTSRANGE], [PROJECTSBACKSRANGE]
         PRINT = DUPLEX
      ENDIF
   ENDIF
ENDIF
