;CARDSIZE = 5.8, 8.8
[card_width] = 12
[card_height] = 9
[duplexbacks]% = _TRUE_
[border_guidelines]% = MARKDOT
[border_type] = NONE
PAGE = [A4LANDSCAPE]
INCLUDE = init.nde

[nofActions] = 10
[costs] = 1|1|1|2|2|3|4|5|6|5
[imgs] = reserve|cement|activate|steel|sell|oil|onemodule|startpack|twomodule|project
[ypos] = 1.3|2.0|2.7|3.4|4.1|4.8|5.5|6.2|6.9|7.6
[xpos] = 1.6
[xpos2] = [xpos] + 0.7
[xpos3] = [xpos2] + 0.7
[xpos4] = [xpos] + 0.8

[playcol] = [red]|[green]|[blue]|[yellow]

[AIDRANGE1]="1-4"
[AIDRANGE2]="9-12"
[AIDRANGE]=RANGEADD([AIDRANGE1], [AIDRANGE2])

[PIECES] = board\pieces\

RECTANGLE = [AIDRANGE], 0, 0, [card_width], [card_height], [playcol], EMPTY, 0.5

;;;;;;;;;;;;;;;;;;;;;
;;; MACROS
;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;
;;; TITLE
;;;;;;;;;;;;;;;;;;;;;
FONT = [FONT_player_aid_title]
<heading1> = 0, 0, 6, 1
TEXT = [AIDRANGE], "Available Actions", <heading1>, CENTER, CENTER

;;;;;;;;;;;;;;;;;;;;;
;;; LIST OF ACTIONS
;;;;;;;;;;;;;;;;;;;;;
FONT = [FONT_player_aid_actions]
FOR=A,1,[nofActions]
   PRINTRESOURCE=[AIDRANGE], 0.2, {ypos?A}, {costs?A}, energy
   IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]rightarrow[IMAGE_FILE_TYPE]", 0.85, {{ypos?A} + 0.2}, 0.4, 0.4, 0, N
   IF={imgs?A}=cement
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]cement[IMAGE_FILE_TYPE]", [xpos], {{ypos?A} + 0.1}, 0.5, 0.5, 0, N
   ELSEIF={imgs?A}=steel
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]steel[IMAGE_FILE_TYPE]", [xpos], {{ypos?A} + 0.1}, 0.5, 0.5, 0, N
   ELSEIF={imgs?A}=oil
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]oil[IMAGE_FILE_TYPE]", [xpos], {ypos?A}, 0.6, 0.6, 0, N
   ELSEIF={imgs?A}=activate
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]activate[IMAGE_FILE_TYPE]", [xpos], {{ypos?A} + 0.1}, 0.4, 0.5, 0, N
   ELSEIF={imgs?A}=reserve
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]activate[IMAGE_FILE_TYPE]", [xpos], {{ypos?A} + 0.1}, 0.4, 0.5, 0, N
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]padlock[IMAGE_FILE_TYPE]", {[xpos] + 0.4}, {{ypos?A} + 0.05}, 0.6, 0.6, 0, N
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]project[IMAGE_FILE_TYPE]", {[xpos] + 1}, {{ypos?A} + 0.2}, 0.6238, 0.4, 0, N
   ELSEIF={imgs?A}=sell
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]vp[IMAGE_FILE_TYPE]", [xpos], {ypos?A}, 0.6, 0.6, 0, NP
   ;ELSEIF={imgs?A}=mixed
      ;PRINTMIXEDOFFER = [AIDRANGE], 1, {ypos?A}
   ELSEIF={imgs?A}=onemodule
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]module[IMAGE_FILE_TYPE]", [xpos], {ypos?A}, 0.4, 0.6238, 0, N
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]activate[IMAGE_FILE_TYPE]", {[xpos] + 0.6}, {{ypos?A} + 0.1}, 0.4, 0.5, 0, N
   ELSEIF={imgs?A}=startpack
      PRINTRESOURCE = [AIDRANGE], [xpos], {ypos?A}, 5, cement
      PRINTRESOURCE = [AIDRANGE], [xpos2], {ypos?A}, 4, steel
      PRINTRESOURCE = [AIDRANGE], [xpos3], {ypos?A}, 3, oil
   ELSEIF={imgs?A}=twomodule
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]module[IMAGE_FILE_TYPE]", [xpos], {ypos?A}, 0.4, 0.6238, 0, N
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]module[IMAGE_FILE_TYPE]", {[xpos] + 0.6}, {ypos?A}, 0.4, 0.6238, 0, N
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]activate[IMAGE_FILE_TYPE]", {[xpos] + 1.2}, {{ypos?A} + 0.1}, 0.4, 0.5, 0, N
   ELSEIF={imgs?A}=project
      IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]project[IMAGE_FILE_TYPE]", [xpos], {{ypos?A} + 0.2}, 0.6238, 0.4, 0, N
      PRINTRESOURCE = [AIDRANGE], [xpos4], {ypos?A}, 7, energy
      FONT = [FONT_player_aid_actionsexplained]
      TEXT = [AIDRANGE], "(     in a 2 player       game)", {[xpos] + 0.8}, {{ypos?A} + 0.2}, 2.5, 1, LEFT, WWTOP
   ENDIF
NEXT

<mixed> = 2.7, 2.35, 3.1, 2.5
RECTANGLE = [AIDRANGE], <mixed>, #000000, EMPTY, 0.04
;LINE = [AIDRANGE], <mixed, PTR, PTL>, #000000, 0.04
;LINE = [AIDRANGE], <mixed, PTL, PBL>, #000000, 0.04
;LINE = [AIDRANGE], <mixed, PBL, PBR>, #000000, 0.04
IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]4players[IMAGE_FILE_TYPE]", 3.615, 2.65, 1.27, 0.59, 0, N
FONT = [FONT_player_aid_actions]
PRINTRESOURCE=[AIDRANGE], 2.9, 3.65, 3, energy
IMAGE = [AIDRANGE], "[PATH_TO_IMAGES]rightarrow[IMAGE_FILE_TYPE]", 3.6, 3.85, 0.4, 0.4, 0, N
PRINTMIXEDOFFER = [AIDRANGE], 4.2, 3.65


[ypos2] = 1|2.1|2.9|3.7|4.5|5.7|7.2
[xpos4] = 6.5
[xpos5] = 7

{[phases1] = Select Actions Phase
|Reveal Actions Phase
|Pay Energy
|Perform Actions Phase
|Resupply Phase
|Check for game end
|
}
{[shorts1] = play face down, wait for everyone else to do the same
|pick up your old card first
|least Energy left pays first
|most Energy left acts first
|turn the resupply disc, resupply Offer spaces & markets
|Energy: one more turn\13\Modules/Projects: immediate end
|
}

{[phases2] = Select Actions Phase
|Reveal Actions Phase
|Pay Energy
|Perform Actions Phase
|Resupply Phase
|Event Phase
|Check for game end
}
{[shorts2] = play face down, wait for everyone else to do the same
|pick up your old card first
|least Energy left pays first
|most Energy left acts first
|turn the resupply disc, resupply Offer spaces & markets
|discard the Current Event, move the Next Event to Current Event, draw a new Next Event
|Energy: one more turn\13\Modules/Projects: immediate end
}


;;;;;;;;;;;;;;;;;;;;;
;;; TITLE
;;;;;;;;;;;;;;;;;;;;;
<heading2> = 6, 0, 6, 1
FONT = [FONT_player_aid_title]
TEXT = [AIDRANGE1], "Turn Sequence", <heading2>, CENTER, CENTER
TEXT = [AIDRANGE2], "Turn Sequence", <heading2>, CENTER, CENTER

;;;;;;;;;;;;;;;;;;;;;
;;; PHASES AND EXPLANATIONS
;;;;;;;;;;;;;;;;;;;;;
FOR=B,1,{(phases1)}
   FONT = [FONT_player_aid_phases]
   TEXT = [AIDRANGE1], {phases1?B}, [xpos4], {ypos2?B}, 4.8, 0.7, LEFT, WWTOP
   TEXT = [AIDRANGE2], {phases2?B}, [xpos4], {ypos2?B}, 4.8, 0.7, LEFT, WWTOP
   FONT = [FONT_player_aid_phasesexplained]
   IF = {shorts1?B} <>
      TEXT = [AIDRANGE1], "({shorts1?B})", [xpos5], {{ypos2?B} + 0.5}, 4.6, 1.5, LEFT, WWTOP
   ENDIF
   TEXT = [AIDRANGE2], "({shorts2?B})", [xpos5], {{ypos2?B} + 0.5}, 4.6, 1.5, LEFT, WWTOP
NEXT


;;;;;;;;;;;;;;;;;;;;;
;;; END GAME
;;;;;;;;;;;;;;;;;;;;;
[SCORERANGE1]="5-8"
[SCORERANGE2]="13-16"
[SCORERANGE]=RANGEADD([SCORERANGE1], [SCORERANGE2])

RECTANGLE = [SCORERANGE], 0, 0, [card_width], [card_height], [playcol], EMPTY, 0.5

IMAGE = [SCORERANGE1], "[PIECES]endgame_scoring[IMAGE_FILE_TYPE]", -0.2, 3.5, 5, 5.6, 270, N
IMAGE = [SCORERANGE2], "[PIECES]endgame_scoring_goal[IMAGE_FILE_TYPE]", -0.2, 3.5, 5, 5.6, 270, N
IMAGE = [SCORERANGE], "[PIECES]endgame_tiebreakers[IMAGE_FILE_TYPE]", 3.8, 1.6, 4.4, 7.5, 270, N
IMAGE = [SCORERANGE], "[PIECES]endgame_triggers[IMAGE_FILE_TYPE]", 7.5, 1.1, 4.7, 8, 270, N

;;;;;;;;;;;;;;;;;;;;;
;;; EVENT/GOAL PACKAGE SYMBOL
;;;;;;;;;;;;;;;;;;;;;
PACKAGESYMBOL = [AIDRANGE2], "E"

;;;;;;;;;;;;;;;;;;;;;
;;; DUPLEX
;;;;;;;;;;;;;;;;;;;;;
IF = [duplexbacks]
   ;DUPLEX = [AIDRANGE], "8,7,6,5,16,15,14,13"
   DUPLEX = [AIDRANGE], [SCORERANGE]
   PRINT = DUPLEX
ENDIF
